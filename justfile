# Set default recipe to run when just is called without arguments
default: help

# âš™ï¸ Install the virtual environment and pre-commit hooks
install:
    @echo "âš™ï¸ Creating virtual environment using uv"
    uv sync
    uv run pre-commit install

# âš™ï¸ Install with Rust extensions for optimal performance
install-rust: install
    @echo "ğŸ¦€ Building and installing Rust extensions"
    uv run maturin develop --uv --manifest-path third_party/uf_rush/Cargo.toml

# ğŸ¦€ Build/rebuild Rust extensions only
build-rust:
    @echo "ğŸ¦€ Building Rust union-find extension"
    uv run maturin develop --uv --manifest-path third_party/uf_rush/Cargo.toml

format:
    @echo "ğŸ‘•ï¸ Formatting code with ruff"
    uv run ruff format .

# ğŸ” Run code quality tools (linting, type checking, dependency checks)
check:
    @echo "ğŸ” Checking lock file consistency with 'pyproject.toml'"
    uv lock --locked
    @echo "ğŸ” Linting code: Running pre-commit"
    uv run pre-commit run -a
    @echo "ğŸ” Static type checking: Running mypy"
    uv run mypy
    @echo "ğŸ” Checking for obsolete dependencies: Running deptry"
    uv run deptry src

# ğŸ§ª Test the code with pytest and coverage
test:
    @echo "ğŸ§ª Testing code: Running pytest"
    uv run python -m pytest --cov --cov-config=pyproject.toml --cov-report=xml --cov-report=term-missing

# ğŸ¦€ Test Rust extensions
test-rust:
    @echo "ğŸ¦€ Testing Rust union-find implementation"
    uv run python tests/test_rust_uf.py
    @echo "ğŸ¦€ Running performance comparison"
    uv run python tests/test_union_find_performance.py

# ğŸ“Š Run the gradio app for report visualization
report:
    @echo "ğŸ“Š Running gradio app"
    uv run python -m text_dedup.utils.gradio.run

# ğŸ§¹ Clean build artifacts
clean-build:
    @echo "ğŸ§¹ Removing build artifacts"
    rm -rf ./dist

clean: clean-build
    @echo "ğŸ§¹ Cleaning cache and build artifacts"
    find . -name "*cache*" -type d -exec rm -rf {} +
    rm -rf coverage.xml

# ğŸ§¹ Clean Rust build artifacts
clean-rust:
    @echo "ğŸ¦€ Cleaning Rust build artifacts"
    cd third_party/uf_rush && cargo clean
    rm -rf third_party/uf_rush/target/
    rm -rf third_party/uf_rush/python/uf_rush/*.so

# ğŸ—ï¸ Build wheel file
build: clean-build
    @echo "ğŸ—ï¸ Creating wheel file"
    uvx --from build pyproject-build --installer uv

# ğŸ“¤ Publish release to PyPI
publish:
    @echo "ğŸ“¤ Publishing to PyPI"
    uvx twine upload --repository-url https://upload.pypi.org/legacy/ dist/*

# ğŸš¢ Build and publish in one step
build-and-publish: build publish

# ğŸ’¡ Show this help message
help:
    @just --list --unsorted

run:
    @echo "ğŸƒï¸ Run with the config file"
    uv run --frozen -m text_dedup.minhash

app:
    @echo "ğŸƒï¸ Run gradio app"
    uv run --with gradio,plotly,gradio_rangeslider -m text_dedup.utils.gradio.run
